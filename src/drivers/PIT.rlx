; Int 0x20, IRQ0

#Require "./src/utility/x64.rlx"
#Require "./src/kernel/core/Events.rlx"

struct PITCounterMode {
	i64 WholePerTick
	i64 FractionPerTick
	i64 FractionToWholeDivisor
	i16 ReloadValue
	
	define i64 GuessHZ() {
		return 1000 / this->WholePerTick
	}
}

PITCounterMode PIT_100HZ_CONFIG {
	; I really wish I'd wrote down how I got these numbers
	
	WholePerTick: 			10, 
	FractionPerTick: 		6872852, 
	FractionToWholeDivisor: 1000000000,
	ReloadValue:			11931
}

PITCounterMode* PIT_100HZ := &PIT_100HZ_CONFIG

i64 PITWhole 	:= 0 ; Number of milliseconds since the PIT was initialized
i64 PITFraction := 0 ; Counter mode specific fraction, used to track sub-millisecond time since PITWhole is an integer
i64 TickCount	:= 0 ; Number of unknown length ticks since the PIT was initialized

define i64 GetTickCount() {
	return TickCount
}
define i64 GetUptimeMS() {
	return PITWhole
}

PITCounterMode* CurrentPITMode := null

Event _OnTimerTick {
	Name: "PIT Tick"
}
Event* OnHardwareTimerTick := &_OnTimerTick

define void OnPITInterrupt(InterruptContext* Context) {
	TickCount += 1

	PITWhole += CurrentPITMode->WholePerTick
	PITFraction += CurrentPITMode->FractionPerTick
	
	PITWhole += PITFraction / CurrentPITMode->FractionToWholeDivisor
	PITFraction %= CurrentPITMode->FractionToWholeDivisor
	
	;Info("T")
	
	if (DEBUG_SLOW_CLOCK) {
		if (TickCount % DEBUG_SLOW_CLOCK = 0) {
			OnHardwareTimerTick->Fire(PITWhole As void*)
		}
	}
	else {
		OnHardwareTimerTick->Fire(PITWhole As void*)
	}
	
	SendEOI(0)
}
define void PITInterrupt() asm {
	call, @InterruptSetup
	call, @OnPITInterrupt
	jmp, @InterruptReturn
}

i8 PIT_DATA_PORTS_BASE := 0x40
i8 PIT_COMMAND_PORT := 0x43

i8 PIT_CHANNEL_0 := 0
i8 PIT_CHANNEL_1 := 1
i8 PIT_CHANNEL_2 := 2

i8 PIT_ACCESS_LATCH := 0
i8 PIT_ACCESS_LOWBYTE := 1
i8 PIT_ACCESS_HIGHBYTE := 2
i8 PIT_ACCESS_BOTHBYTE := 3

i8 PIT_MODE_RATE_GENERATOR := 6

i8 PC_SPEAKER_PORT := 0x61
i8 PC_SPEAKER_PIT_MASK := 0b1
i8 PC_SPEAKER_STATE_MASK := 0b10
i8 PC_SPEAKER_ENABLE := PC_SPEAKER_PIT_MASK | PC_SPEAKER_STATE_MASK
i8 PC_SPEAKER_DISABLE := ~PC_SPEAKER_ENABLE

define void InitializePITChannel(i8 Channel, i8 AccessMode, i8 OperatingMode, i16 Reload) {
	i8 Command := ((Channel << 6) | (AccessMode << 4) | (OperatingMode)) As i8

	OutB(PIT_COMMAND_PORT, Command) 
	OutB(PIT_DATA_PORTS_BASE + Channel, (Reload & 0xFF) As i8)
	OutB(PIT_DATA_PORTS_BASE + Channel, (Reload >> 8) As i8)
}

define void SetupPIT() {
	InsertIDTEntry(0x20, &PITInterrupt, true)
}

DeviceInfo* PIT := CreateDevice("PIT", &SetupPIT)

define void SetPITMode(PITCounterMode* Mode) {
	CurrentPITMode := Mode
	
	InitializePITChannel(PIT_CHANNEL_0, PIT_ACCESS_BOTHBYTE, PIT_MODE_RATE_GENERATOR, Mode->ReloadValue)
	
	EnablePICLine(0, true)
}

define void SleepTicks(i32 Ticks) {
	i64 End := TickCount + Ticks
	
	if (IsSchedulerActive()) {
		while (TickCount < End) {}
	}
	else {
		while (TickCount < End) { SchedulerYield() }
	}
}
define void SleepMS(i32 Milliseconds) {
	i64 End := PITWhole + Milliseconds
	
	if (IsSchedulerActive()) {
		while (PITWhole < End) { SchedulerYield() }
	}
	else {
		while (PITWhole < End) {}
	}
}

define void PlayRawSound(i32 Counter) {
	InitializePITChannel(PIT_CHANNEL_2, PIT_ACCESS_BOTHBYTE, PIT_MODE_RATE_GENERATOR, Counter As i16)

	i8 State := InB(PC_SPEAKER_PORT)

	if !(State & PC_SPEAKER_ENABLE) {
		OutB(PC_SPEAKER_PORT, State | PC_SPEAKER_ENABLE)
	}
}
define void PlaySound(i32 Frequency) {
	PlayRawSound(1193180 / Frequency)
}

define void StopSound() {
	OutB(PC_SPEAKER_PORT, InB(PC_SPEAKER_PORT) & PC_SPEAKER_DISABLE)
}