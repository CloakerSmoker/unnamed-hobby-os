
i16 MASTER_PIC := 0x20
i16 SLAVE_PIC := 0xA0

i16 MASTER_PIC_DATA := 0x21
i16 SLAVE_PIC_DATA  := 0xA1

i32 PIC_MASTER_OFFSET
i32 PIC_SLAVE_OFFSET

define void RemapPIC(i8 MasterOffset, i8 SlaveOffset) {
	PIC_MASTER_OFFSET := MasterOffset
	PIC_SLAVE_OFFSET := SlaveOffset
	
	i8 OldMaster := InB(MASTER_PIC_DATA)
	i8 OldSlave := InB(SLAVE_PIC_DATA)
	
	OutB(MASTER_PIC, 0x11)
	OutB(SLAVE_PIC, 0x11)
	
	OutB(MASTER_PIC_DATA, MasterOffset)
	OutB(SLAVE_PIC_DATA, SlaveOffset)
	
	OutB(MASTER_PIC_DATA, 0x04)
	OutB(SLAVE_PIC_DATA, 0x02)
	
	OutB(MASTER_PIC_DATA, 0x01)
	OutB(SLAVE_PIC_DATA, 0x01)
	
	OutB(MASTER_PIC_DATA, 0xFF As i8)
	OutB(SLAVE_PIC_DATA, 0xFF As i8)
}

define i32 IRQToIDT(i32 IRQ) {
	if (LocalAPIC->IsEnabled) {
		return IRQ
	}

	if (IRQ >= 8) {
		return PIC_SLAVE_OFFSET + IRQ - 8
	}
	else {
		return PIC_MASTER_OFFSET + IRQ
	}
}

define void SendEOI(i8 IRQLine) {
	if (LocalAPIC->IsEnabled) {
		LocalAPIC->EOI()
		return
	}
	
	if (IRQLine >= 8) {
		OutB(SLAVE_PIC, 0x20)
	}
	
	OutB(MASTER_PIC, 0x20)
}

define void EnablePICLine(i8 IRQLine, i8 Enabled) {
	if (LocalAPIC->IsEnabled) {
		FirstIOAPIC->ForwardInterrupt(IRQLine, LocalAPIC->ID, PIC_MASTER_OFFSET + IRQLine)
		
		return
	}
	
	i16 Port := MASTER_PIC_DATA
	
	if (IRQLine >= 8) {
		Port := SLAVE_PIC_DATA
		IRQLine -= 8
	}
	
	i32 Value := InB(Port)
	
	if (Enabled) {
		Value &= ~(1 << IRQLine)
	}
	else {
		Value |= (1 << IRQLine)
	}
	
	OutB(Port, Value As i8)
}

define void DisablePIC() {
	OutB(MASTER_PIC_DATA, 0)
	OutB(SLAVE_PIC_DATA, 0)
}