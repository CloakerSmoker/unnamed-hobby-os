
#Require "./src/kernel/networking/Endianess.rlx"

i16 ETHERTYPE_ARP := 0x0806

struct EthernetHeader {
    MACAddress Destination
    MACAddress Source
    i16 EtherType
    i8[0] Payload

    define void FlipEndianess() {
        this->EtherType := NetworkShort(this->EtherType)
    }
}

i32 PACKET_DATA_ETHERNET := RegisterPacketData("Ethernet Header")

#Require "$/AVLTree.rlx"

struct EthernetLayer {
    NetworkInterface* Interface

    AVLTree* EtherTypeHandlers ; <i16, NetworkProtocol>

    define NetworkPacket* Enter(MACAddress* Destination, i16 EtherType) {
        NetworkPacket* Packet := NetworkPacket:New(PAGE_SIZE)

        EthernetHeader* Header := Packet->Mark(PACKET_DATA_ETHERNET, #EthernetHeader)

        Header->EtherType := EtherType
        Header->Source->Set(this->Interface->MAC)
        Header->Destination->Set(Destination)

        return Packet
    }

    define void Send(NetworkPacket* Packet) {
        EthernetHeader* Header := Packet->Get(PACKET_DATA_ETHERNET)

        Header->FlipEndianess()

        this->Interface->Send(Packet->Data, Packet->Size)
    }

    define void Ingest(void* RawPacket, i32 Size) {
        EthernetHeader* Header := RawPacket

        Header->FlipEndianess()

        if !(Header->Destination->Equals(this->Interface->MAC)) {
            return
        }

        NetworkProtocol* Protocol := this->EtherTypeHandlers->GetPointer(Header->EtherType)

        if !(Protocol) {
            Info("Packet from %mac has unknown ethertype %x\n", Header->Source, Header->EtherType)
            return
        }

        NetworkPacket* Packet := NetworkPacket:New(RawPacket, Size)

        Packet->Mark(PACKET_DATA_ETHERNET, #EthernetHeader)

        Protocol->Ingest(Packet)
    }

}