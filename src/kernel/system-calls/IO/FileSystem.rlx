define i64 SystemCallOpen(i8* Path, i32 Flags, i32 Mode) {
	if (Path = null) {
		; wtf busybox vi?
		
		return -LINUX_ERROR_FAULT
	}
	
	i8 Ext2Flags := EXT2_OPEN_IF_EXISTS
	
	if (Flags & LINUX_FILE_CREATE_NEW) {
		Ext2Flags := EXT2_OPEN_CREATE
	}

	if (CurrentProcess->WorkingDirectory->TypeID() != typeid:(GenericDirectoryFileHandle)) {
		Panic("Process working directory is not a GenericDirectoryFileHandle")
	}

	GenericDirectoryFileHandle* WorkingDirectoryHandle := CurrentProcess->WorkingDirectory
	IDirectory* WorkingDirectory := WorkingDirectoryHandle->Directory
	
	VFSDirectoryEntry Entry
	if !(Lookup(WorkingDirectory, Path, &Entry)) {
		return -LINUX_ERROR_NOENT
	}

	IFileHandle* EntryHandle := null

	if (Entry.Type = VFSDirectoryEntryType::File) {
		EntryHandle := Entry.File->Open(Ext2Flags)
	}
	else if (Entry.Type = VFSDirectoryEntryType::Directory) {
		EntryHandle := new GenericDirectoryFileHandle(Entry.Directory)
	}
	
	return CurrentProcess->AllocateFileDescriptor(EntryHandle)
}

define i64 SystemCallClose(i64 FileDescriptor) {
	CurrentProcess->FileDescriptorsToHandles[FileDescriptor]->Close()
	CurrentProcess->FileDescriptorsToHandles[FileDescriptor] := null
	
	return 0
}

define i64 SystemCallDeviceControl(i64 FileDescriptor, i32 Command, void* Argument) {
	return CurrentProcess->FileDescriptorsToHandles[FileDescriptor]->DeviceControl(Command, Argument)
}

define i64 SystemCallFileStatus(i64 FileDescriptor, LinuxFileStatus* Out) {
	CurrentProcess->FileDescriptorsToHandles[FileDescriptor]->Status(Out As FileStatus*)
	
	return 0
}

define i64 SystemCallPathStatus(i8* Path, LinuxFileStatus* Out) {
	;Info("Stat(%s, %x)\n", Path, Out)
	
	IFileHandle* Target := CurrentProcess->WorkingDirectory->Open(Path)
	
	if !(Target) {
		return -LINUX_ERROR_NOENT
	}
	
	Target->Status(Out As FileStatus*)
	Target->Close()
	
	return 0
}

define i64 SystemCallTruncate(i64 FileDescriptor, i32 Size) {
	return 0
}

define i64 SystemCallSeek(i64 FileDescriptor, i32 Offset, i8 Mode) {
	return CurrentProcess->FileDescriptorsToHandles[FileDescriptor]->Seek(Offset, Mode As SeekMode)
}

define i32 SystemCallPoll(LinuxPollTarget* Targets, i32 TargetCount, i32 Timeout) {
	if (Timeout < 0) {
		Timeout := 0x7FFF_FFFF
	}

	i64 End := GetUptimeMS() + Timeout
	
	while (GetUptimeMS() <= End) {
		i32 CountWithReturnEvents := 0

		for (i32 Index := 0, Index < TargetCount, Index++) {
			LinuxPollTarget* TargetInfo := Targets[Index]
			
			IFileHandle* Target := CurrentProcess->FileDescriptorsToHandles[TargetInfo->FileDescriptor]

			;Info("Poll(%i, %b) = ", TargetInfo->FileDescriptor, TargetInfo->RequestedEvents)
			TargetInfo->ReturnedEvents := Target->Poll(TargetInfo->RequestedEvents) As i16

			;Info("%b\n", TargetInfo->ReturnedEvents)

			if (TargetInfo->ReturnedEvents) {
				CountWithReturnEvents += 1
			}
		}

		if (CountWithReturnEvents) {
			return CountWithReturnEvents
		}

		SchedulerYield()
	}

	return 0
}

define i64 SystemCallRead(i64 FileDescriptor, i8* Buffer, i32 Size) {
	IFileHandle* Handle := CurrentProcess->FileDescriptorsToHandles[FileDescriptor]
	
	i64 ActualSize := Handle->Read(Buffer, Size)

	if (DEBUG_SYS_READ) {
		Info("sys_read(%x, %x) -> %x\n%hexdump\n", FileDescriptor, Size, ActualSize, Buffer, ActualSize)
	}

	return ActualSize
}

define i64 SystemCallWrite(i64 FileDescriptor, i8* Buffer, i32 Size) {
	if (FileDescriptor = 1 || FileDescriptor = 2) {
		if (DEBUG_SYS_WRITE) {
			Info("%hexdump", Buffer, Size)
		}
	}
	
	IFileHandle* Handle := CurrentProcess->FileDescriptorsToHandles[FileDescriptor]
	
	return Handle->Write(Buffer, Size)
}

define i64 SystemCallSendFile(i64 Out, i64 In, i64* Offset, i64 Size) {
	;Print("SendFile(%x, %x, %x, %x)\n", Out, In, Offset, Size)
	
	i64 Count := Size
	i64 Written := 0

	void* Temp := KernelPageAllocator->AllocateVirtual(2)
	i32 ChunkSize := 2 * PAGE_SIZE

	while (Count != 0) {
		i32 ThisChunkSize := ChunkSize

		if (ThisChunkSize > Count) {
			ThisChunkSize := Count As i32
		}

		i64 ThisChunkRead := SystemCallRead(In, Temp, ThisChunkSize)
		
		;Info("Read %x/%x bytes\n", ThisChunkRead, ThisChunkSize)
		
		i64 ThisChunkWritten := SystemCallWrite(Out, Temp, ThisChunkRead As i32)

		;Info("Written %x/%x bytes\n", ThisChunkWritten, ThisChunkSize)

		if (ThisChunkRead != ThisChunkSize || ThisChunkWritten != ThisChunkSize) {
			break
		}

		Written += ThisChunkSize
		Count -= ThisChunkSize
	}

	KernelPageAllocator->FreeVirtual(Temp, 2)

	return Written
}

define i64 SystemCallWriteMultiple(i64 FileDescriptor, void* Data, i32 Count) {
	i64 TotalWritten := 0
	
	LinuxIOVector* Vectors := Data
	
	for (i32 Index := 0, Index < Count, Index++) {
		LinuxIOVector* Next := Vectors[Index]
		
		TotalWritten += SystemCallWrite(FileDescriptor, Next->Base, Next->Size As i32)
	}
	
	return TotalWritten
}

define void RegisterFileSystemSystemCalls() {
	SystemCalls->Register(LinuxSyscall::open, &SystemCallOpen)
	SystemCalls->Register(LinuxSyscall::close, &SystemCallClose)
	SystemCalls->Register(LinuxSyscall::ioctl, &SystemCallDeviceControl)
	SystemCalls->Register(LinuxSyscall::fstat, &SystemCallFileStatus)
	SystemCalls->Register(LinuxSyscall::stat, &SystemCallPathStatus)
	SystemCalls->Register(LinuxSyscall::ftruncate, &SystemCallTruncate)
	SystemCalls->Register(LinuxSyscall::lseek, &SystemCallSeek)
	SystemCalls->Register(LinuxSyscall::poll, &SystemCallPoll)
	SystemCalls->Register(LinuxSyscall::read, &SystemCallRead)
	SystemCalls->Register(LinuxSyscall::write, &SystemCallWrite)
	SystemCalls->Register(LinuxSyscall::sendfile, &SystemCallSendFile)
	SystemCalls->Register(LinuxSyscall::writev, &SystemCallWriteMultiple)
}