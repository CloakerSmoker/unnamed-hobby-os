
struct ShellCommand {
    i8* Name,
    void(i64, i8**) Callback
}

i32 CommandCount := 0
ShellCommand[20] Commands

define i32 RegisterCommand(i8* Name, void(i64, i8**) Callback) {
    ShellCommand* Command := Commands[CommandCount++]

    Command->Name := Name
    Command->Callback := Callback

    return CommandCount
}

define ShellCommand* LookupCommand(i8* Name) {
    for (i32 Index := 0, Index < CommandCount, Index++) {
        ShellCommand* Check := Commands[Index]

        if (StringEquals(Check->Name, Name)) {
            return Check
        }
    }

    return null
}

IDirectory* CurrentDirectory := null
IDirectory* DevicesDirectory := null

#Require "./src/kernel/file-system/VFS/shell/Utilities.rlx"

#Require "./src/kernel/file-system/VFS/shell/Common.rlx"
#Require "./src/kernel/file-system/VFS/shell/Loop.rlx"
#Require "./src/kernel/file-system/VFS/shell/Mount.rlx"
#Require "./src/kernel/file-system/VFS/shell/GPT.rlx"

define void VFSExecuteShellCommand(i8* Line) {
    i64 ArgC := 0
    i8*[10] ArgV

    ParseCommandLine(Line, &ArgC, ArgV)
    i8* CommandName := ArgV[0]

    ShellCommand* Command := LookupCommand(CommandName)

    if (Command = null) {
        Print("'%s' command not found\n", CommandName)
    }
    else {
        Command->Callback(ArgC, ArgV)
    }
}

define void VFSRunShell(FileHandle* stdio) {
    CurrentDirectory := VFSRootDirectory
    DevicesDirectory := VFSDevicesDirectory

    i8[200] LineBuffer

    loop {
        Print(">")
        i32 Length := stdio->Read(LineBuffer, 200)
        LineBuffer[Length - 1] := 0

        VFSExecuteShellCommand(LineBuffer)
    }
}