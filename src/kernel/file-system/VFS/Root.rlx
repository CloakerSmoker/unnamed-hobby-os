aIDirectory* VFSRootOverlayDirectory := null
aIDirectory* VFSRootDirectory := null

define void MountRoot(aIDirectory* NewRoot) {
    aIDirectory* RootOverlay := VFSRootOverlayDirectory

    if (RootOverlay = null) {
        RootOverlay := VFSDirectory:New()
    }

    aIDirectory* Root := VFSOverlayDirectory:New(NewRoot, RootOverlay, VFSOverlayWriteMode::OnlyEphemeralToOverlay) As aIDirectory*

    VFSRootOverlayDirectory:= RootOverlay
    VFSRootDirectory := Root
}

enum FilePathAnchor {
    None := 0,
    Root := 1
}

struct FilePath {
    FilePathAnchor Anchor

    i8*[10] Parts
    i32 Count

    i8[256] Buffer

    i8* FileName

    define void Shift() {
        this->Count -= 1
        this->FileName := this->Parts[this->Count - 1]
    }

    define void UnShift() {
        this->Count += 1
        this->FileName := this->Parts[this->Count - 1]
    }

    define void Zero() {
        FastSetMemory8(this, 0, #self)
    }

    define i8* CloneFileName() {
        return CloneMemory(this->FileName, StringLength(this->FileName))
    }

    define i32 Append(i32 Start, i32 End) {
        this->Parts[this->Count++] := this->Buffer + Start
        this->Buffer[End] := 0

        return End + 1
    }

    define self* Seal() {
        this->FileName := this->Parts[this->Count - 1]

        return this
    }

    define void Print() {
        if (this->Anchor = FilePathAnchor::Root) {
            Print("/")
        }

        for (i32 Index := 0, Index < this->Count - 1, Index++) {
            Print("%s/", this->Parts[Index])
        }

        Print("%s", this->FileName)
    }
}

define void ParsePath(i8* Path, i32 Length, FilePath* Out) {
    Out->Zero()

    if (Path[0] = '/') {
        Out->Anchor := FilePathAnchor::Root

        Path += 1
        Length -= 1
    }
    else {
        Out->Anchor := FilePathAnchor::None
    }

    MoveMemory(Out->Buffer, Path, Length)

    i32 Start := 0

    for (i32 Index := 0, Index < Length, Index++) {
        if (Path[Index] = '/') {
            Start := Out->Append(Start, Index)
        }
    }

    Out->Append(Start, Length)

    Out->Seal()
}

define void ParsePath(i8* Path, FilePath* Out) {
    ParsePath(Path, StringLength(Path), Out)
}

define i8 Lookup(aIDirectory* At, FilePath* Path, VFSDirectoryEntry* Result) {
    aIDirectory* Current := At

    if (Path->Anchor = FilePathAnchor::Root) {
        if (Path->Count = 0) {
            Result->Type := VFSDirectoryEntryType::Directory
            Result->Name := "/"
            Result->Directory := Current

            return true
        }

        Current := VFSRootDirectory
    }

    for (i32 Index := 0, Index < Path->Count - 1, Index++) {
        i8* Part := Path->Parts[Index]

        VFSDirectoryEntry Next
        
        if !(Current->Lookup(Part, &Next)) {
            return false
        }

        if (Next.Type != VFSDirectoryEntryType::Directory) {
            return false
        }
        
        Current := Next.Directory
    }

    return Current->Lookup(Path->FileName, Result)
}

define i8 Lookup(aIDirectory* At, i8* RawPath, VFSDirectoryEntry* Result) {
    FilePath Path
    ParsePath(RawPath, &Path)

    return Lookup(At, &Path, Result)
}

define i8 Mount(aIDirectory* At, i8* RawPath, aIDirectory* Root) {
    FilePath Path
    ParsePath(RawPath, &Path)

    if (Path.Anchor = FilePathAnchor::Root) {
        At := VFSRootDirectory
    }

    aIDirectory* Current := At

    for (i32 Index := 0, Index < Path->Count - 1, Index++) {
        i8* Part := Path->Parts[Index]
        i8 ShouldCreateNew := true

        VFSDirectoryEntry Next
        if (Current->Lookup(Part, &Next)) {
            if (Next.Type != VFSDirectoryEntryType::Hole) {
                ShouldCreateNew := false
            }
        }

        if (ShouldCreateNew) {
            aIDirectory* NewChild := VFSDirectory:New()

            VFSDirectoryEntry NewChildEntry {
                Type: VFSDirectoryEntryType::Directory,
                Name: CloneString(Part),
                Directory: NewChild
            }

            Current->Insert(&NewChildEntry)

            Current := NewChild
        }
        else {
            ; Entry exists, but we need to make sure it is ephemeral

            aIDirectory* Entry := Next.Directory

            if (Entry->IsEphemeral()) {
                Current := Entry
            }
            else {
                ; Entry isn't ephemeral, we need to replace it with an overlay based on itself

                aIDirectory* Base := Entry
                aIDirectory* Overlay := VFSDirectory:New()
                aIDirectory* Replacement := VFSOverlayDirectory:New(Base, Overlay, VFSOverlayWriteMode::OnlyEphemeralToOverlay)

                VFSDirectoryEntry OverlayEntry {
                    Type: VFSDirectoryEntryType::Directory,
                    Name: CloneString(Part),
                    Directory: Replacement
                }

                Current->Insert(&OverlayEntry)

                Current := Replacement
            }
        }
    }

    VFSDirectoryEntry NewEntry {
        Type: VFSDirectoryEntryType::Directory,
        Name: CloneString(Path->FileName),
        Directory: Root
    }

    if (Current->TypeID() = typeid:(VFSOverlayDirectory)) {
        return (Current As VFSOverlayDirectory*)->Overlay->Insert(&NewEntry)
    }
    else {
        return Current->Insert(&NewEntry)
    }
}

define aIDirectory* LookupParentDirectory(aIDirectory* At, FilePath* Path) {
    if (Path->Count = 1) {
        if (Path->Anchor = FilePathAnchor::Root) {
            return VFSRootDirectory
        }
        else {
            return At
        }
    }

    Path->Shift()

    VFSDirectoryEntry ParentEntry
    if !(Lookup(At, Path, &ParentEntry)) {
        return null
    }

    Path->UnShift()

    if (ParentEntry.Type != VFSDirectoryEntryType::Directory) {
        return null
    }

    return ParentEntry.Directory
}

define aIDirectory* CreateDirectory(aIDirectory* At, i8* RawPath) {
    FilePath Path
    ParsePath(RawPath, &Path)

    if (Path.Anchor = FilePathAnchor::Root) {
        At := VFSRootDirectory
    }

    aIDirectory* Parent := LookupParentDirectory(At, &Path)
    aIDirectory* Result := Parent->CreateDirectory()

    VFSDirectoryEntry Entry {
        Type: VFSDirectoryEntryType::Directory,
        Name: CloneString(Path.FileName),
        Directory: Result
    }

    Parent->Insert(&Entry)

    return Result
}

define aIFile* CreateFile(aIDirectory* At, i8* RawPath) {
    FilePath Path
    ParsePath(RawPath, &Path)

    if (Path.Anchor = FilePathAnchor::Root) {
        At := VFSRootDirectory
    }

    aIDirectory* Parent := LookupParentDirectory(At, &Path)
    aIFile* Result := Parent->CreateFile()

    VFSDirectoryEntry Entry {
        Type: VFSDirectoryEntryType::File,
        Name: CloneString(Path.FileName),
        File: Result
    }

    Parent->Insert(&Entry)

    return Result
}