
#Require "./src/kernel/multitasking/Tasks.rlx"
#Require "./src/kernel/core/Events.rlx"

struct Timer {
	i64 LastFiredAt
	i8 Enabled

	i8 OneShot
	i64 IntervalMS

	union {
		Task* Target
		Task* Task
	}
	
	Timer* Next
	
	define void MaybeFire(i64 TimeMS) {
		if (this->Enabled && (TimeMS - this->LastFiredAt) >= this->IntervalMS) {
			this->Target->State := TASK_CAN_RUN
			
			;Info("\nFire %x\n", this->Target)

			if (this->OneShot) {
				this->Enabled := false
			}
			
			this->LastFiredAt := TimeMS
		}
	}

	define void Enable() {
		this->Enabled := true
	}
	define void Disable() {
		this->Enabled := false
	}

	define void SetOneShot(i64 Interval) {
		this->Enabled := true
		this->OneShot := true

		this->LastFiredAt := GetUptimeMS()
		this->IntervalMS := Interval
	}
}

Timer* RootTimer := null

define EventState OnTick(void* RawCurrentWhole, Event* Fired) {
	i64 CurrentWhole := RawCurrentWhole As i64
	
	Timer* NextTimer := RootTimer
	
	while (NextTimer) {
		NextTimer->MaybeFire(CurrentWhole)
		NextTimer := NextTimer->Next
	}
	
	return EventState:CallNextListener
}

define void SetupTimers() {
	OnHardwareTimerTick->ListenFunction(&OnTick)
}

define Timer* AllocateTimer(Task* Target) {
	Timer* Result := Alloc(#Timer)
	
	Result->Target := Target
	
	if (RootTimer) {
		Timer* Last := RootTimer
		
		while (Last->Next) {
			Last := Last->Next
		}
		
		Last->Next := Result
	}
	else {
		RootTimer := Result
	}

	return Result
}

define Timer* SetTimer(i64 IntervalMS, Task* Target) {
	Timer* Result := AllocateTimer(Target)

	Result->IntervalMS := IntervalMS
	Result->Enabled := true
	
	return Result
}