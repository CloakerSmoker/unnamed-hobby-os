
enum EventState {
	CallNextListener
	Handled
}

enum EventHandlerKind {
	Function
	Method
}

struct EventListener {
	EventListener* Next
	
	union {
		EventState(void*, Event*) Function
		EventState(void*, void*, Event*) Method
	}
	
	void* Parameter
	
	EventHandlerKind Kind
}

struct Event {
	EventListener* FirstListener
	i8* Name
	i32 FireCounter
	
	define EventListener* ListenMethod(EventState(void*, void*, Event*) Method, void* Parameter) {
		EventListener* Result := Alloc(#EventListener)
		
		Result->Method := Method
		Result->Parameter := Parameter
		Result->Kind := EventHandlerKind:Method
		
		Result->Next := this->FirstListener
		this->FirstListener := Result
		
		return Result
	}
	define EventListener* ListenFunction(EventState(void*, Event*) Function) {
		EventListener* Result := Alloc(#EventListener)
		
		Result->Function := Function
		Result->Kind := EventHandlerKind:Function
		
		Result->Next := this->FirstListener
		this->FirstListener := Result
		
		return Result
	}
	
	define void RemoveListener(EventListener* Listener) {
		if (this->FirstListener = Listener) {
			this->FirstListener := Listener->Next
		}
		else if (this->FirstListener) {
			EventListener* Current := this->FirstListener
			
			while (Current) {
				if (Current->Next = Listener) {
					Current->Next := Listener->Next
				}
				
				Current := Current->Next
			}
		}
	}
	
	define void Fire(void* Data) {
		this->FireCounter++
		
		EventListener* Current := this->FirstListener
		
		while (Current) {
			EventState State := EventState:CallNextListener
			
			if (Current->Kind = EventHandlerKind:Function) {
				State := Current->Function(Data, this)
			}
			else if (Current->Kind = EventHandlerKind:Method) {
				State := Current->Method(Current->Parameter, Data, this)
			}
			
			if (State = EventState:Handled) {
				break
			}
			
			Current := Current->Next
		}
	}
	
	define void Wait() {
		i64 OriginalCounter := this->FireCounter
		
		while (this->FireCounter = OriginalCounter) {
			SchedulerYield()
		}
	}
	
	define void Free() {
		EventListener* Current := this->FirstListener
		
		while (Current) {
			EventListener* Last := Current
			Current := Current->Next
			
			Free(Last)
		}
	}
}

/*
struct Semaphore {
	i32 Count
	PointerArray* Waiters

	static self* New() {
		self* this := 
	}

	define void Wait() {
		DisableInterrupts()

		this->Count -= 1

		if (this->Count < 0) {
			this->Waiters->Push(CurrentTask)
			SchedulerBlock()
		}

		EnableInterrupts()
	}

	define void WaitTimeout(i32 Timeout) {
		DisableInterrupts()

		this->Count -= 1

		if (this->Count < 0) {
			this->Waiters->Push(CurrentTask)
			SchedulerBlock()
		}

		EnableInterrupts()
	}

	define void Signal() {
		DisableInterrupts()

		if (this->Count < 0) {
			Task* Waiter := this->Waiters->Pop()

			Waiter->Unblock()
		}

		this->Count += 1

		EnableInterrupts()
	}
}
*/