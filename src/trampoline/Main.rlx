

; Loaded with the leftover EFI identity map

; Kernel ELF pointer is passed

/*
We need to get things set up for the kernel, mainly:

* Physical memory allocator (0x1000 onwards)
* Kernel text physical memory
* Kernel page tables
* Kernel Stack
* Kernel Heap
* Kernel page mappings

<128mb all identity mapped

*/

#ImageBase 0x80_0000
void* TRAMPOLINE_ADDRESS := 0x80_0000 As void*

#Require "$/MemoryBase.rlx"

#Require "@/PrintBase.rlx"
#Require "@/ExceptionBase.rlx"

#Require "./src/utility/x64.rlx"
#Require "./src/kernel/lib/Exit.rlx"

#Require "./src/bootloader/BootInfo.rlx"
#Require "./src/trampoline/Info.rlx"

#Require "./src/kernel/Config.rlx"

#Require "./src/utility/ELF.rlx"

#Require "./src/kernel/graphics/PixelBuffer.rlx"
#Require "./src/kernel/core/PhysicalMemoryManager.rlx"

define i64 IdentityVirtualToPhysical(void* Pointer) {
    return Pointer As i64
}

declare void MainContinuation(TrampolineInfo*)

define void Main(BootloaderInfo* RawInfo) {
    ; nuke IDT to kill TianoCore

    asm {
		xor, rax, rax
		push, rax
		push, rax
		mov, rax, rsp
		lidt, {rax} As i64*
		pop, rax
		pop, rax
	}

    FastZeroMemory(&COM_0, #SerialPort)
    FastZeroMemory(&Terminal, #EarlyTerminal)

    COM_0.Initialize(COM_PORT_0, COM_DIVISOR_115200, COM_LINE_8N1)

    Terminal.LoadFON(i8[#File "./misc/fonts/OlivettiThin_8x14.fon"])
    Terminal.SetFrameBuffer(RawInfo->PhysicalFrameBuffer As void*, RGB32, 1024, 768)
    Terminal.Clear()

    i64 MaxAddress := RawInfo->MaxAddress
    i64 MaxPage := (MaxAddress / PAGE_SIZE) As i32

    Print("Maximum Physical Address: %x\n", MaxAddress)
    Print("Maximum Physical    Page: %x\n", MaxPage)

    i64 TotalPhysicalPages := (MaxAddress / PAGE_SIZE) + 1

    i64 AllocationMapBytes := TotalPhysicalPages / 8
    i64 ManagerPageCount := SizeToPageCount(AllocationMapBytes As i32) + 1

    Print("Searching for a %x page chunk to hold physical page map\n", ManagerPageCount)

    i64 TrampolineBytes := RawInfo->TrampolineMaxAddress - (TRAMPOLINE_ADDRESS As i64)
    i32 TrampolinePages := SizeToPageCount(TrampolineBytes As i32)

    i64 TrampolineStart := TRAMPOLINE_ADDRESS As i64
    i64 TrampolineEnd := TrampolineStart + (TrampolinePages * PAGE_SIZE)

    i64 KernelStart := RawInfo->KernelData As i64
    i64 KernelEnd := KernelStart + (SizeToPageCount(RawInfo->KernelSize) * PAGE_SIZE)

    void* PhysicalManagerBuffer := null

    i32 Count := RawInfo->AvailableCount

    Print("%i regions\n", Count)

    for (i32 Index := 0, Index < Count, Index++) {
        AvailableMemoryRegion* Region := RawInfo->AvailableMemory[Index]

        i64 Start := Region->Base
        i64 End := Start + (Region->Size * PAGE_SIZE)

        if (TrampolineStart >= Start && TrampolineEnd <= End) {
            Print("%x - %x: Overlaps trampoline\n", Start, End)

            ; Skip this region if it contains the trampoline since we can't properly "split"
            ;  the region to share it between the trampoline and physical allocator

            continue
        }
        else if (KernelStart >= Start && KernelEnd <= End) {
            Print("%x - %x: Overlaps kernel\n", Start, End)
            continue
        }

        if (Region->Size >= ManagerPageCount + 1) {
            ; Skip the first page of the region for funsies

            PhysicalManagerBuffer := (Region->Base + PAGE_SIZE) As void*
            break
        }
        else {
            Print("%x - %x: Not large enough for allocation map\n", Start, End)
        }
    }

    if (PhysicalManagerBuffer = null) {
        Panic("Not enough contiguous free memory for physical memory allocation map")
    }

    PhysicalMemoryManager* PhysicalMemory := PhysicalMemoryManager:Adopt(PhysicalManagerBuffer, MaxAddress)

    PhysicalMemory->AllocateAll()

    ;PhysicalMemory->Display(Terminal~>FrameBuffer, 400, 0, 500)

    i32 Count := RawInfo->AvailableCount

    for (i32 Index := 0, Index < Count, Index++) {
        AvailableMemoryRegion* Region := RawInfo->AvailableMemory[Index]

        PhysicalMemory->Free(Region->Base, Region->Size)
    }

    Print("Allocating %x - %x for physical memory bitmap\n", PhysicalManagerBuffer, PhysicalManagerBuffer + (ManagerPageCount * PAGE_SIZE))

    PhysicalMemory->Allocate(PhysicalManagerBuffer As i64, ManagerPageCount)

    Print("Allocating %x - %x for trampoline\n", TRAMPOLINE_ADDRESS, TRAMPOLINE_ADDRESS + (TrampolinePages * PAGE_SIZE))

    PhysicalMemory->Allocate(TRAMPOLINE_ADDRESS As i64, TrampolinePages)

    ; Reserve super low memory
    PhysicalMemory->Allocate(0, 0x10)

    ; Allocate the TrampolineInfo (and populate it from the BootInfo)

    TrampolineInfo* Info := PhysicalMemory->AllocateVirtual(1)
    FastZeroMemory(Info, PAGE_SIZE)
    MoveMemory(Info, RawInfo, #BootloaderInfo)
    BootloaderInfo* BootInfo := Info~>BootInfo

    Info->PhysicalMemory := PhysicalMemory

    Print("Instance Info: %x\n", Info)

    ; Relocate kernel data into memory we manage
    ; (We'll end up freeing this once we've mapped the kernel into memory)
    ; (It's just too annoying to map the kernel without virtual memory properly wired up)

    void* NewKernelData := PhysicalMemory->AllocateVirtual(SizeToPageCount(BootInfo->KernelSize))
    MoveMemory(NewKernelData, BootInfo->KernelData, BootInfo->KernelSize)

    BootInfo->KernelData := NewKernelData

    Print("Relocated Kernel File: %x\n", BootInfo->KernelData)

    ; Set up the PhysicalPageAllocator

    PhysicalPageAllocator* Physical := Info~>PhysicalMemoryAllocator
    Physical->Initialize()
    Physical->Manager := PhysicalMemory

    ; Set up the AddressSpaceManager

    void* PML4 := PhysicalMemory->AllocateVirtual(1)
    FastZeroMemory(PML4, PAGE_SIZE)

    Print("PML4: %x\n", PML4)

    AddressSpaceManager* VirtualMemory := Info~>VirtualMemory
    VirtualMemory->Initialize(PML4, IdentityVirtualToPhysical(PML4), Physical)

    PageTrie* Tree := Info~>PageTree
    Tree->Initialize(PML4, Physical)

    ; Identity map the framebuffer
    void* FrameBuffer := Info~>BootInfo->PhysicalFrameBuffer As void*

    ;VirtualMemory->MapRange(FrameBuffer, IdentityVirtualToPhysical(FrameBuffer) / PAGE_SIZE, SizeToPageCount(1024 * 786 * 4), PAGE_PRESENT | PAGE_WRITE)
    Tree->SetRange(FrameBuffer, IdentityVirtualToPhysical(FrameBuffer), SizeToPageCount(1024 * 786 * 4), PAGE_PRESENT | PAGE_WRITE)
    Print("%x -> %x\n", FrameBuffer, IdentityVirtualToPhysical(FrameBuffer))

    ; Identity map all physical memory
    ;VirtualMemory->MapRange(null, 0, (MaxPage - 1) As i32, PAGE_PRESENT | PAGE_WRITE)
    Tree->SetRange(null, 0, (MaxPage - 1) As i32, PAGE_PRESENT | PAGE_WRITE)

    i64 KernelStackPhysical := PhysicalMemory->Allocate(PAGES_FOR_KERNEL_STACK)
    ;VirtualMemory->MapRange(ADDRESS_OF_KERNEL_STACK, KernelStackPhysical / PAGE_SIZE, PAGES_FOR_KERNEL_STACK, PAGE_PRESENT | PAGE_WRITE)
    Tree->SetRange(ADDRESS_OF_KERNEL_STACK, KernelStackPhysical, PAGES_FOR_KERNEL_STACK, PAGE_PRESENT | PAGE_WRITE)

    void* KernelStack := ADDRESS_OF_KERNEL_STACK + (PAGE_SIZE * (PAGES_FOR_KERNEL_STACK - 1))

    i64 State := Tree->StartWalkMappings()
    VirtualMapping Mapping

    while (Tree->WalkMappingsNext(&State, &Mapping)) {
        Print("%x..%x -> %x..%x (%x pages)\n", Mapping.Virtual, Mapping.Virtual + Mapping.Size, Mapping.Physical, Mapping.Physical + Mapping.Size, Mapping.Size / PAGE_SIZE)
    }

   ;VirtualMemory->PrintMappings()

    SetCR4(GetCR4() | CR4_ENABLE_PAGE_SIZE)

    ;VirtualMemory->Enable(&MainContinuation, Info, KernelStack)

    SetCR3(Tree->Root As i64, &MainContinuation, Info, KernelStack)
}

define void SetupHeap(TrampolineInfo* Info, void* VirtualAddress, i32 PageCount) {
    i64 HeapPhysicalPages := Info->PhysicalMemory->Allocate(PageCount)

    Info~>PageTree->SetRange(VirtualAddress, HeapPhysicalPages, PageCount, PAGE_PRESENT | PAGE_WRITE)
    FastZeroMemory(VirtualAddress, PageCount * PAGE_SIZE)

    Info~>Heap->Initialize(VirtualAddress, PageCount * PAGE_SIZE)

    Print("Heap Initialized\n")
    Info~>Heap->Bootstrap()

    Print("Heap Bootstrapped!\n")
}

define void* MapKernel(TrampolineInfo* Info, void* KernelFileData, i32 KernelFileSize) {
    ELFHeader* Image := KernelFileData
    ELFProgramHeader* ProgramHeaders := Image->GetProgramHeaders()

    for (i32 Index := 0, Index < Image->ProgramHeaderEntryCount, Index++) {
		ELFProgramHeader* Next := ProgramHeaders[Index]
		
		if (Next->Type = ELF_SEGMENT_TYPE_LOAD) {
			void* SectionData := Image + Next->FileOffset
			void* SectionMemory := Next->VirtualAddress As void*
			void* SectionVirtualPage := SectionMemory & 0xFFFF_FFFF_FFFF_F000
			
			;Print("Section %i is %i (%x) bytes @ %x\n", Index, Next->MemorySize, Next->MemorySize, SectionMemory)
			
			i32 SectionPageCount := (Next->MemorySize As i32) / PAGE_SIZE
			
			if (Next->MemorySize % PAGE_SIZE) {
				SectionPageCount += 1
			}
			
			if (Next->FileOffset % PAGE_SIZE) {
				SectionPageCount += 1
			}
			
			if (SectionPageCount = 0) {
				continue
			}
			
			i64 TempFlags := PAGE_PRESENT | PAGE_WRITE ; Map as PRESENT|WRITE so we can... actually load the section into memory
			i64 FinalFlags := PAGE_PRESENT | PAGE_USER
			
			if (Next->Flags & ELF_SEGMENT_FLAGS_WRITE) {
				FinalFlags |= PAGE_WRITE
			}

            i64 Physical := Info->PhysicalMemory->Allocate(SectionPageCount)

            ;Print("Allocated %i pages @ %x for section\n", SectionPageCount, Physical)

            Info~>PageTree->SetRange(SectionVirtualPage, Physical, SectionPageCount, TempFlags)
			
			if (Next->MemorySize > Next->FileSize) {
				void* ZeroArea := SectionMemory + Next->FileSize
				i64 ZeroSize := Next->MemorySize - Next->FileSize
				
				FastSetMemory8(ZeroArea, 0, ZeroSize)
			}
			
			MoveMemory(SectionMemory, SectionData, Next->FileSize)

            Info~>PageTree->SetRange(SectionVirtualPage, Physical, SectionPageCount, FinalFlags)
		}
	}

    return Image->EntryPoint As void*
}

define void MainContinuation(TrampolineInfo* Info) {
    Info("Continuation\n")

    ; PageTree is active, must invlpg changes
    Info~>PageTree->ShouldInvalidate := true

    SetupHeap(Info, ADDRESS_OF_KERNEL_HEAP, PAGES_FOR_KERNEL_HEAP)
    Info("heap!\n")

    void(TrampolineInfo*) KernelEntryPoint := MapKernel(Info, Info~>BootInfo->KernelData, Info~>BootInfo->KernelSize)

    Info("Kernel Main: %x\n", KernelEntryPoint)

    ELFHeader* KernelELF := Info~>BootInfo->KernelData

    ELFSymbol* PreMainSymbol := KernelELF->FindSymbol("PreMain(TrampolineInfo*)")
    void(TrampolineInfo*) PreMain := KernelELF->GetSymbolAddress(PreMainSymbol)

    Print("Kernel PreMain: %x\n", PreMain)

    PreMain(Info)

    Print("Returned from PreMain\n")

    PageTrie* Tree := Info~>PageTree

    i64 State := Tree->StartWalkMappings()
    VirtualMapping Mapping

    while (Tree->WalkMappingsNext(&State, &Mapping)) {
        i64 Pages := Mapping.Size / PAGE_SIZE

        Print("%x..%x -> %x..%x (%x pages)\n", Mapping.Virtual, Mapping.Virtual + Mapping.Size, Mapping.Physical, Mapping.Physical + Mapping.Size, Pages)
    }

    ;Info->PhysicalMemory->Display(Terminal~>FrameBuffer, 400, 0, 500)
    ;Halt()

    KernelEntryPoint(Info)

    loop {}
}