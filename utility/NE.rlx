
struct MZHeaderNE {
	i8 MagicM,
	i8 MagicZ,
	i16 BytesInLastPage,
	i16 PageCount,
	i16 RelocationCount,
	
	i16 HeaderParagraphCount,
	i16 MinimumExtraParagraphCount,
	i16 MaximumExtraParagraphCount,
	i16 StartingSS,
	
	i16 StartingSP,
	i16 Checksum,
	i16 StartingIP,
	i16 StartingCS,
	
	i16 RelocationTableOffset,
	i16 OverlayNumber,
	i16 Padding1,
	i16 Padding2,
	
	i16 OEMID,
	i16 OEMInfo,
	i16 Padding3,
	i16 Padding4,
	
	i64 Padding5,
	
	i64 Padding6,
	
	i32 Padding7,
	i32 NEHeaderOffset
}

struct NEHeader unaligned {
	i8[2] Signature
	
	i8 MajorLinkerVersion
	i8 MinorLinkerVersion
	
	i16 EntryTableOffset
	i16 EntryTableLength
	i32 FileCRC
	i8 PorgramFlags
	i8 ApplicationFlags
	
	i16 DataSegmentIndex
	
	i16 InitialHeapSize
	i16 InitialStackSize
	
	i32 EntryPoint
	i32 InitialStack
	
	i16 SegmentCount
	i16 ModuleReferences
	
	i16 ExternalNamesTableSize
	
	i16 SegmentTablesOffset
	i16 ResourcesTablesOffset
	i16 LocalNamesTableOffset
	i16 ModuleReferenceTableOffset
	i16 ImportNamesTableOffset
	i16 ExternalNamesTableOffset
	
	i16 MoveableEntryPointCount
	i16 FileAlignment
	i16 ResourceTableEntryCount
	
	i8 TargetOS
	i8 OS2Flags
	
	i16 ReturnThunksOffset
	i16 SegmentReferenceThunksOffset
	
	i16 MinimumCodeSwapSize
	i8[2] ExpectedWindowsVersion
}

i32 NE_RESOURCE_TYPE_ID_FONT_DIRECTORY := 0x8007
i32 NE_RESOURCE_TYPE_ID_FONT           := 0x8008

struct NEResource {
	i16 FileOffset
	i16 Size
	i16 Flags
	i16 ID
	i32 Reserved
	
	define i32 FileOffset() {
		return (this->FileOffset As i32) & 0xFFFF
	}
	define i32 Size() {
		return (this->Size As i32) & 0xFFFF
	}
	define i32 ID() {
		return (this->ID As i32) & 0xFFFF
	}
}

struct NEResourceTableEntry unaligned {
	i16 TypeID
	i16 ResourceCount
	i32 Reserved
	NEResource[0] Resources
	
	define i32 TypeID() {
		return (this->TypeID As i32) & 0xFFFF
	}
	
	define NEResource* GetEntry(i32 TargetIndex) {
		return this->Resources[TargetIndex]
	}
	
	define NEResourceTableEntry* Next() {
		return this + #NEResourceTableEntry + this->ResourceCount * #NEResource
	}
}

struct NEResourceTableHeader {
	i16 AlignmentShiftCount
	NEResourceTableEntry[0] Entries
	
	define NEResourceTableEntry* GetEntry(i32 TargetIndex) {
		NEResourceTableEntry* Current := this->Entries[0]
		
		for (i32 Index := 0, Index < TargetIndex, Index++) {
			Current := Current->Next()
		}
		
		return Current
	}
	
	define void* End() {
		NEResourceTableEntry* Current := this->Entries[0]
		
		while (Current->TypeID() != 0) {
			Current := Current->Next()
		}
		
		return Current As void*
	}
}

struct NEFile {
	union {
		void* MZData
		MZHeaderNE* MZHeader
	}
	
	union {
		void* Data
		NEHeader* Header
	}
	
	NEResourceTableHeader* Resources
	
	i32 Size
	
	define void Initialize(void* Data, i32 Size) {
		this->MZData := Data
		this->Size := Size
		
		this->Header := Data + this->MZHeader->NEHeaderOffset
		this->Resources := this->Data + ((this->Header->ResourcesTablesOffset As i32) & 0xFF)
	}
	
	define NEResource* FindResource(i32 Type, i32 ResourceIndex) {
		for (i32 Index := 0, Index < this->Header->ResourceTableEntryCount, Index++) {
			NEResourceTableEntry* Entry := this->Resources->GetEntry(Index)
			
			i32 TypeID := Entry->TypeID()
			
			if (TypeID = 0) {
				break
			}
			
			if (TypeID != Type) {
				continue
			}
			
			return Entry->GetEntry(ResourceIndex)
		}
		
		return null
	}
	
	define void* GetResourceData(NEResource* Resource) {
		return this->MZData + (Resource->FileOffset() << (this->Resources->AlignmentShiftCount As i8))
	}
	define i32 GetResourceSize(NEResource* Resource) {
		return Resource->Size() << (this->Resources->AlignmentShiftCount As i8)
	}
	
}