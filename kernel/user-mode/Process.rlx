#Require "./src/utility/ELF.rlx"
#Require "./src/kernel/core/MemoryMapping.rlx"
#Require "./src/linux/Signals.rlx"
#Require "./src/linux/ErrorNumbers.rlx"

declare void HandleSystemCall(AMD64Context*)

define i64 SignalMask(i32 Number) {
	return (1 As i64) << ((Number & 0xFF) As i8)
}

struct ProcessSignalContainer {
	LinuxSignalAction[31] Actions
	
	i32[31] PendingErrorNumbers
	i32[31] PendingSignalCodes
	
	i64 PendingSignals
	
	define void Queue(i32 Number, i32 Error, i32 Code) {
		this->PendingSignals |= SignalMask(Number)
		
		this->PendingErrorNumbers[Number] := Error
		this->PendingSignalCodes[Number] := Code
	}
	
	define i32 Claim(i64 ThreadMask, i32* OutErrorNumber, i32* OutSignalCode) {
		i64 CanFireMask := this->PendingSignals & ~ThreadMask
		
		if !(CanFireMask) {
			return -1
		}
		
		for (i32 Index := 0, Index < 31, Index++) {
			i64 Mask := SignalMask(Index)
			
			if (CanFireMask & Mask) {
				*OutErrorNumber := this->PendingErrorNumbers[Index]
				*OutSignalCode := this->PendingSignalCodes[Index]
				
				this->PendingSignals -= Mask
				
				return Index
			}
		}
		
		return -1
	}
}

UserThread* CurrentThread := null

struct UserThread {
	Task* UserTask ; Just holds state
	Task* KernelTask ; Actually scheduled
	AMD64Context AbortSystemCallState
	
	ProcessSignalContainer* ParentSignals
	
	i32 ParentProcessID
	
	i8 HasPendingSystemCall
	
	i64 SignalMask
	
	LinuxSignalAction* CurrentSignalAction
	LinuxSignalInfo* CurrentSignalInfo
	LinuxSignalUserContext* CurrentSignalContext
	i32 CurrentSignal
	
	i32 ExitCode
	Event OnKilled
	
	declare void SetupSignalState(i32, i32, i32)
	
	define void AbortSystemCall() {
		this->UserTask~>Context->RAX := LINUX_ERROR_INTR
		this->HasPendingSystemCall := false
		
		MoveMemory(this->KernelTask~>Context, this~>AbortSystemCallState, #AMD64Context)
	}
	
	define void DoKernelThread() {
		; Since this thread "manually" yeilds to the user-mode task, we need to do a bit
		;  of a dance to make sure that control ends up in the right place when this thread
		;   is interrupted and resumed
		
		; Each time this thread resumes, we need to do a few things:
		;  1) Handle system calls
		;  2) Dispatch signals (TODO)
		; and finally
		;  3) Resume the user task
		
		; We can get interrupted during 1/2 which will save our context, but once we reach
		;  3 there's nothing left to save our context, making a bit of a problem.
		
		; We need to persist any interrupted state during 1/2, but need to reset state back
		;  to the top of this function before 3 (otherwise the next time we're resumed, then
		;   we'd actually end up resuming previous code which was already interrupted and
		;    likely finished executing)
		
		; This is accomplished by only saving the "top of function" state into the actual
		;  task once steps 1/2 are completely done and we know that any state in our task
		;   is just garbage.
		
	
		SaveContext(this~>AbortSystemCallState)
		
		i32 SignalErrorNumber := 0
		i32 SignalCode := 0
		i32 Signal := this->ParentSignals->Claim(this->SignalMask, &SignalErrorNumber, &SignalCode)
		
		if (Signal != -1) {
			; We've got a signal we can fire on this thread, modify this->UserTask so
			;  it'll jump into a signal handler with the current state on the user stack
			
			; Technically, we might also have a syscall going, which I think Linux has
			;  a thingy for (E_RESTART or whatever). I'm not smart enough to know why
			;   but I think we can skip that.
			
			this->SetupSignalState(Signal, SignalErrorNumber, SignalCode)
			
			if !(this->KernelTask->CouldRun()) {
				SchedulerEnd()
			}
		}
		else if (this->HasPendingSystemCall) {
			EnableInterrupts()
			HandleSystemCall(this->UserTask~>Context)
			DisableInterrupts()
			
			if !(this->KernelTask->CouldRun()) {
				SchedulerEnd()
			}
			
			this->HasPendingSystemCall := false
		}
		
		MoveMemory(this->KernelTask~>Context, this~>AbortSystemCallState, #AMD64Context)
		
		CurrentThread := this
		
		Scheduler->RunNow(this->UserTask)
	}
	
	define void Exit(i32 Code) {
		this->ExitCode := Code
		this~>OnKilled->Fire(this)
		
		DisableInterrupts()
		this->KernelTask->State := TASK_KILLED
		EnableInterrupts()
		
		;KernelHeap->Free(CurrentProcess)
		;Mappings->Delete()
	}
	
	define void DoDefaultSignalAction(i32 Number, i32 Error, i32 SignalCode) {
		this->Exit(Number)
	}
	
	define void SetupSignalState(i32 Number, i32 Error, i32 SignalCode) {
		i64 Mask := SignalMask(Number)
		i64 OldMask := this->SignalMask
		
		LinuxSignalAction* Action := this->ParentSignals->Actions[Number]
		
		if (Action->Handler = LINUX_SIGNAL_HANDLER_DEFAULT) {
			this->DoDefaultSignalAction(Number, Error, SignalCode)
			
			return
		}
		else if (Action->Handler = LINUX_SIGNAL_HANDLER_IGNORE) {
			return
		}
		
		this->SignalMask |= Mask
		this->SignalMask |= Action->Mask
		
		AMD64Context* Context := this->UserTask~>Context
		
		void* OriginalRSP := Context->RSP
		
		LinuxSignalInfo* Info := Context->StackAllocate(#LinuxSignalInfo)
		
		Info->SignalNumber := Number
		Info->Error := Error
		Info->SignalCode := SignalCode
		
		LinuxSignalUserContext* UserContext := Context->StackAllocate(#LinuxSignalUserContext)
		
		LinuxSignalRegisters* Registers := UserContext~>Registers
		LinuxSignalStack* Stack := UserContext~>Stack
		
		Registers->PopulateFromContext(Context)
		Registers->RSP := OriginalRSP
		
		UserContext->Mask := OldMask
		
		if (Action->Flags & LINUX_SIGNAL_ATTRIBUTE_RESTORER) {
			Context->Push(Action->Restorer As i64)
		}
		
		this->CurrentSignal := Number
		this->CurrentSignalAction := Action
		this->CurrentSignalInfo := Info
		this->CurrentSignalContext := UserContext
		
		Context->RDI := Number
		Context->RSI := Info As i64
		Context->RDX := UserContext As i64
		Context->RIP := Action->Handler
	}
	
	define void SignalReturn() {
		this->CurrentSignalContext~>Registers->PopulateContext(this->UserTask~>Context)
		this->SignalMask := this->CurrentSignalContext->Mask
		this->CurrentSignal := 0
	}
	
	define void Start(i32 ParentProcessID, ProcessSignalContainer* ParentSignals, void* Stack, void* Code) {
		this->ParentProcessID := ParentProcessID
		this->ParentSignals   := ParentSignals
		
		this->UserTask~>Context->RSP := Stack
		this->UserTask~>Context->RIP := Code
		this->UserTask->NeedsUserMode := true
		
		this->KernelTask~>Context->RIP := &UserThread.DoKernelThread
		this->KernelTask~>Context->RDI := this As i64
		this->KernelTask~>Context->Flags := 0
		
		Scheduler->Enqueue(this->KernelTask)
	}
	
	define void FireSystemCall() {
		this->HasPendingSystemCall := true
		
		Scheduler->RunNow(this->KernelTask)
	}
}

struct Process {
	Ext2Handle*[10] FileDescriptorsToHandles
	i32 FileDescriptorCount
	
	ELFHeader* ELF
	MemoryMappingGroup* Mappings
	PageHeap* PageAllocator
	
	void* BreakStart
	i64 BreakPages
	
	UserThread MainThread
	
	i32 TotalThreads
	i32 AliveThreads
	
	Event OnKilled
	
	i32 ExitCode
	
	define EventState OnThreadKilled(UserThread* Thread, Event* Fired) {
		this->AliveThreads -= 1
		
		if (this->AliveThreads = 0) {
			this->ExitCode := Thread->ExitCode
			this~>OnKilled->Fire(this)
		}
		
		return EventState:CallNextListener
	}
	
	define void Initialize(ELFHeader* Header, MemoryMappingGroup* Mappings) {
		this->ELF := Header
		this->Mappings := Mappings
		
		i64 MaxAddress := 0
	
		ELFProgramHeader* ProgramHeaders := Header->GetProgramHeaders()

		for (i32 Index := 0, Index < Header->ProgramHeaderEntryCount, Index++) {
			ELFProgramHeader* NextProgramHeader := ProgramHeaders[Index]
			
			if (NextProgramHeader->Type = ELF_SEGMENT_TYPE_LOAD) {
				i64 NextEnd := NextProgramHeader->VirtualAddress + NextProgramHeader->MemorySize
				
				if (NextEnd > MaxAddress) {
					MaxAddress := NextEnd
				}
			}
		}
		
		this->BreakStart := ((MaxAddress + PAGE_SIZE) & 0x7FFF_FFFF_FFFF_F000) As void*
		this->BreakPages := 0
		
		this~>MainThread->UserTask   := Task:New(0)
		this~>MainThread->KernelTask := Task:NewWithStack(0, 1)
		
		this->TotalThreads := 1
		this->AliveThreads := 1
		
		this~>MainThread~>OnKilled->ListenMethod(&Process.OnThreadKilled, this)
	}
	
	define void* GetBreak() {
		return this->BreakStart + (this->BreakPages * PAGE_SIZE)
	}
	define void* SetBreak(void* NewBreakEnd) {
		i32 AdditionalBreakPages := ((NewBreakEnd - this->GetBreak()) As i32) / PAGE_SIZE
		
		void* FirstNewBreakPage := this->GetBreak()
		
		this->Mappings->AllocateAddress(FirstNewBreakPage, AdditionalBreakPages, PAGE_USER | PAGE_WRITE)
		
		SetMemory(FirstNewBreakPage, AdditionalBreakPages * PAGE_SIZE, 0)
		
		this->BreakPages += AdditionalBreakPages
		
		return this->GetBreak()
	}
	
	define void ExitAll(i32 Code) {
		this~>MainThread->Exit(Code)
	}
	
	ProcessSignalContainer Signals
	
	define void QueueSignal(i32 Number, i32 Error, i32 Code) {
		this~>Signals->Queue(Number, Error, Code)
		
		if (this~>MainThread->HasPendingSystemCall) {
			this~>MainThread->AbortSystemCall()
		}
	}
	
	INode* WorkingDirectory
}

Process* CurrentProcess := null