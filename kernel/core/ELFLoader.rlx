#Require "./src/utility/ELF.rlx"

struct ELFMemoryMapping {
	void* Virtual
	i64 Physical
	i32 PageCount
	ELFMemoryMapping* Next
}

define ELFMemoryMapping* MapELF(AddressSpaceManager* AddressSpace, ELFHeader* Image) {
	ELFMemoryMapping* First := null
	ELFMemoryMapping** LastNext := &First
	
	ELFProgramHeader* ProgramHeaders := Image->GetProgramHeaders()
	
	for (i32 Index := 0, Index < Image->ProgramHeaderEntryCount, Index++) {
		ELFProgramHeader* Next := ProgramHeaders[Index]
		
		if (Next->Type = ELF_SEGMENT_TYPE_LOAD) {
			void* SectionData := Image + Next->FileOffset
			void* SectionMemory := Next->VirtualAddress As void*
			void* SectionVirtualPage := SectionMemory & 0xFFFF_FFFF_FFFF_F000
			
			Print("Section %i is %i (%x) bytes\n", Index, Next->MemorySize, Next->MemorySize)
			
			i32 SectionPageCount := (Next->MemorySize As i32) / PAGE_SIZE
			
			if (Next->MemorySize % PAGE_SIZE) {
				SectionPageCount += 1
			}
			
			if (Next->FileOffset % PAGE_SIZE) {
				SectionPageCount += 1
			}
			
			if (SectionPageCount = 0) {
				continue
			}
			
			i64 SectionPhysicalPages := PhysicalMemory->Reserve('ELF', SectionPageCount * PAGE_SIZE, PAGE_SIZE)
			
			Print("Allocated %i pages starting at %x for section %i of image\n", SectionPageCount, SectionPhysicalPages / PAGE_SIZE, Index)
			
			Print("Pages are PRESENT | USER")
			
			i64 Flags := PAGE_PRESENT | PAGE_USER
			
			if (Next->Flags & ELF_SEGMENT_FLAGS_WRITE) {
				Print(" | WRITE ")
				Flags |= PAGE_WRITE
			}
			
			Print("\n")
			
			Print("Mapping virtual(%x) to physical(%x)\n", SectionMemory & 0xFFFF_FFFF_FFFF_F000, SectionPhysicalPages)
			
			KernelAddressSpace->MapRange(SectionVirtualPage, SectionPhysicalPages / PAGE_SIZE, SectionPageCount, Flags)
			
			ELFMemoryMapping* Mapping := KernelHeap->Allocate('ELFM', #ELFMemoryMapping)
			
			Mapping->Virtual := SectionVirtualPage
			Mapping->Physical := SectionPhysicalPages
			Mapping->PageCount := SectionPageCount
			
			*LastNext := Mapping
			LastNext := Mapping~>Next
			
			if (Next->MemorySize > Next->FileSize) {
				void* ZeroArea := SectionMemory + Next->FileSize
				i64 ZeroSize := Next->MemorySize - Next->FileSize
				
				FastSetMemory8(ZeroArea, 0, ZeroSize)
			}
			
			FastMoveMemory(SectionMemory, SectionData, Next->FileSize)
			
			Print("Wrote %x bytes to section memory\n\n", Next->MemorySize)
		}
	}
	
	return First
}

define void UnmapELF(AddressSpaceManager* AddressSpace, ELFMemoryMapping* Next) {
	while (Next != null) {
		AddressSpace->UnmapRange(Next->Virtual, Next->PageCount)
		
		ELFMemoryMapping* Last := Next
		
		Next := Next->Next
		
		KernelHeap->Free(Last)
	}
}