#ImageBase 0x34000


#Require "./src/utility/Generic.rlx"
#Require "./src/utility/x64.rlx"
#Require "./src/utility/String.rlx"

i8 USE_BOCHS_PORT_HACK := true

#Require "./src/drivers/Bochs.rlx"

declare void* KernelHeapAllocate(i64, i32)
declare void* KernelHeapExtendAllocation(void*, i32)
declare void KernelHeapFree(void*)

#Require "./src/kernel/graphics/Font.rlx"
#Require "./src/kernel/graphics/Screen.rlx"
#Require "./src/kernel/graphics/Terminal.rlx"

#Require "./src/kernel/core/Paging.rlx"
#Require "./src/kernel/core/Heap.rlx"

Heap RawKernelHeap
Heap* KernelHeap := &RawKernelHeap

define void* KernelHeapAllocate(i64 Tag, i32 Size) {
	return KernelHeap->Allocate(Tag, Size)
}
define void* KernelHeapExtendAllocation(void* Memory, i32 Size) {
	return KernelHeap->Extend(Memory, Size)
}
define void KernelHeapFree(void* Memory) {
	KernelHeap->Free(Memory)
}

#Require "./src/kernel/core/Interrupts.rlx"
#Require "./src/kernel/core/Devices.rlx"

#Require "./src/utility/Memory.rlx"
#Require "./src/utility/Exception.rlx"
#Require "./src/drivers/Ext2.rlx"

Ext2DeviceTraits* RootDevice := null
Ext2* FS := null
INode* RootINode := null

#Require "./src/kernel/core/Shell.rlx"

define void Main(void* RawFrameBuffer) {
	BochsPrintString("Main\n")
	
	BochsPrint("Len: %i\n", StringLength("\n"))
	
	AddressSpace->Takeover()
	
	BochsPrintString("Paging initialized, AddressSpaceManager in control\n")
	
	; Identity map 2mb of memory for the kernel heap
	void* HeapMemory := AddressSpace->AllocateMappedPages(PAGE_COUNT_FOR_2MB)
	
	BochsPrintString("Allocated KernelHeap memory\n")
	
	; Remap the next available `FRAMEBUFFER_SIZE` bytes of virtual memory to 0xE000_0000 (or wherever int10 says)
	i32 FrameBufferPageCount := AddressSpace->SizeToPageCount(FRAMEBUFFER_SIZE)
	i64 FrameBufferPage := AddressSpace->AllocateContiguousVirtualPages(FrameBufferPageCount)
	AddressSpace->MapPageRange(FrameBufferPage, AddressSpace->PointerToPageNumber(RawFrameBuffer), FrameBufferPageCount)
	
	i8* FrameBuffer := AddressSpace->PageNumberToPointer(FrameBufferPage) As i8*
	
	;NativeScreen->Adopt(FrameBuffer, SCREEN_WIDTH, SCREEN_HEIGHT, BYTES_PER_PIXEL, 0, 0x00_00_00)
	NativeScreen->Adopt(FrameBuffer, SCREEN_WIDTH, SCREEN_HEIGHT, BYTES_PER_PIXEL, 0, 0xFF_FF_FF)
	
	Terminal->Adopt(NativeScreen)
	Terminal->SetColors(0x00_00_00, 0xFF_FF_FF)
	;Terminal->SetColors(0xFF_FF_FF, 0x00_00_00)
	Terminal->Clear()
	
	Terminal->PrintLine("BOOT: Screen *probably* works")
	
	KernelHeap->Initialize(HeapMemory, BYTES_IN_2MB)
	
	Terminal->Print("BOOT: Kernel Heap Initialized: ")
	KernelHeap->Allocate('Example', 0x222)
	KernelHeap->Print()
	
	SetupInterrupts()
	
	Terminal->SetColors(0x00_00_00, 0xFF_FF_FF)
	
	Terminal->PrintLine("BOOT: Mounting disk")
	
	void* SB := KernelHeapAllocate('SB', 2048)
	void* TI := KernelHeapAllocate('TI', 1024)
	void* TB1 := KernelHeapAllocate('TB1', 1024)
	void* TB2 := KernelHeapAllocate('TB2', 1024)
	void* BMP := KernelHeapAllocate('BMP', 1024)
	
	FS := KernelHeapAllocate('Root', #Ext2)
	RootDevice := Ext2ATADevice:New()
	FS->Traits := RootDevice
	
	FS->Initialize(SB, TI, TB1, TB2, BMP)
	
	RootINode := CloneMemory(FS->ReadINode(2), #INode)
	
	SetupShell()
	
	Prompt()
	
	loop {}
}