#ImageBase 0x34000


#Require "./src/utility/Generic.rlx"
#Require "./src/utility/x64.rlx"
#Require "./src/utility/String.rlx"

i8 USE_BOCHS_PORT_HACK := true

#Require "./src/drivers/Bochs.rlx"

declare void* KernelHeapAllocate(i64, i32)
declare void* KernelHeapExtendAllocation(void*, i32)
declare void KernelHeapFree(void*)

#Require "./src/kernel/graphics/Font.rlx"
#Require "./src/kernel/graphics/Screen.rlx"

#Require "./src/kernel/core/Paging.rlx"
#Require "./src/kernel/core/Heap.rlx"

Heap RawKernelHeap
Heap* KernelHeap := &RawKernelHeap

define void* KernelHeapAllocate(i64 Tag, i32 Size) {
	return KernelHeap->Allocate(Tag, Size)
}
define void* KernelHeapExtendAllocation(void* Memory, i32 Size) {
	return KernelHeap->Extend(Memory, Size)
}
define void KernelHeapFree(void* Memory) {
	KernelHeap->Free(Memory)
}

#Require "./src/kernel/core/Descriptors.rlx"

#Require "./src/utility/Memory.rlx"
#Require "./src/utility/Exception.rlx"

#Require "./src/kernel/core/Timer.rlx"

#Require "./src/kernel/graphics/Terminal.rlx"

i8 USE_INTERRUPT_STUBS := true

#Require "./src/kernel/core/Interrupts.rlx"
#Require "./src/kernel/core/Devices.rlx"

#Require "./src/kernel/core/SystemCalls.rlx"

#Require "./src/drivers/Ext2.rlx"

Ext2DeviceTraits* RootDevice := null
Ext2* FS := null
INode* RootINode := null

#Require "./src/kernel/core/Shell.rlx"

define void Main(void* RawFrameBuffer) {
	BochsPrintString("Main\n")
	
	AddressSpace->Initialize(0x20_0000 As void*, BYTES_IN_6MB / PAGE_SIZE)
	AddressSpace->MapRange(null, 0, PAGE_COUNT_FOR_6MB)
	AddressSpace->Takeover()
	
	BochsPrintString("Paging initialized, AddressSpaceManager in control\n")
	
	; Identity map 2mb of memory for the kernel heap
	void* HeapMemory := 0x60_0000 As void*
	AddressSpace->MapRange(HeapMemory, PAGE_COUNT_FOR_6MB, PAGE_COUNT_FOR_2MB)
	
	BochsPrintString("Allocated KernelHeap memory\n")
	
	; Remap the next available `FRAMEBUFFER_SIZE` bytes of virtual memory to 0xE000_0000 (or wherever int10 says)
	i32 FrameBufferPageCount := AddressSpace->SizeToPageCount(FRAMEBUFFER_SIZE)
	i8* FrameBuffer := 0x80_0000 As i8*
	AddressSpace->MapRange(FrameBuffer, AddressSpace->PointerToPageNumber(RawFrameBuffer), FrameBufferPageCount)
	
	;NativeScreen->Adopt(FrameBuffer, SCREEN_WIDTH, SCREEN_HEIGHT, BYTES_PER_PIXEL, 0, 0x00_00_00)
	NativeScreen->Adopt(FrameBuffer, SCREEN_WIDTH, SCREEN_HEIGHT, BYTES_PER_PIXEL, 0, 0xFF_FF_FF)
	NativeScreen->Clear()
	
	KernelTerminal->Adopt(NativeScreen, 0x00_00_00, 0xFF_FF_FF)
	;KernelTerminal->SetColors(0xFF_FF_FF, 0x00_00_00)
	KernelTerminal->Clear()
	
	KernelTerminal->PrintLine("BOOT: Screen *probably* works")
	
	AddressSpace->Map(0 As void*, 0, PAGE_NOT_PRESENT)
	
	KernelTerminal->PrintLine("BOOT: Unmapped page 0")
	
	KernelHeap->Initialize(HeapMemory, BYTES_IN_2MB)
	
	KernelTerminal->Print("BOOT: Kernel Heap Initialized: ")
	KernelHeap->Allocate('Example', 0x222)
	KernelHeap->Print()
	
	SetupGDT()
	
	KernelTerminal->PrintLine("BOOT: Setup GDT and TSS")
	
	SetupInterrupts()
	
	Print("\nTesting user-mode\n")
	
	SetupSystemCalls()
	Test()
	
	Print("Back in kernel code\n\n")
	
	SetPITMode(PIT_100HZ)
	SetupTimers()
	
	KernelTerminal->SetCursorTimer()
	
	KernelTerminal->PrintLine("BOOT: Mounting disk")
	
	void* SB := KernelHeapAllocate('SB', 2048)
	void* TI := KernelHeapAllocate('TI', 1024)
	void* TB1 := KernelHeapAllocate('TB1', 1024)
	void* TB2 := KernelHeapAllocate('TB2', 1024)
	void* BMP := KernelHeapAllocate('BMP', 1024)
	
	FS := KernelHeapAllocate('Root', #Ext2)
	RootDevice := Ext2ATADevice:New()
	FS->Traits := RootDevice
	
	FS->Initialize(SB, TI, TB1, TB2, BMP)
	
	RootINode := CloneMemory(FS->ReadINode(2), #INode)
	
	SetupShell()
	
	Prompt()
	
	loop {}
}