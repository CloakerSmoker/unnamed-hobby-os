
#Require "./src/kernel/multitasking/Tasks.rlx"
#Require "./src/kernel/core/Events.rlx"

Task* RootTask := null
Task* DelayTask := null

struct TaskScheduler {
	Task* DelayTask
	
	AVLTree* CanRunNow
	AVLTree* CanRunNext
	AVLTree* Blocked
	AVLTree* Stopped
	
	define void RunCurrent() {
		CurrentTask->State := TASK_RUNNING
		CurrentTask->Resume()
	}
	
	define void RunNow(Task* Next) {
		CurrentTask->State := TASK_CAN_RUN
		
		CurrentTask := Next
		this->RunCurrent()
	}
	
	define void Debug(i8* Format, ... Parameters) {
		if (DEBUG_SCHEDULER) {
			RawInfo(Format, Parameters)
		}
	}
	
	define void PickNext() {
		if (InterrupsAreEnabled()) {
			DisableInterrupts()
			
			Info("SCHEDULER CALLED WITH INTERRUPTS ON\n")
			Info("Stack:\n%stackdump\nHalting")
			
			loop {}
		}
		
		Task* Current := RootTask
		
		while (Current && !Current->CouldRun()) {
			Current := Current->Next
		}
		
		if !(Current) {
			CurrentTask := DelayTask
			
			this->Debug("\n[%i.%i] %x -> ", PITWhole, PITFraction, CurrentTask)
		
			if (DEBUG_SCHEDULER) {
				Task* Walk := RootTask
				
				while (Walk) {
					this->Debug("(%x %i)", Walk, Walk->State)
					
					Walk := Walk->Next
					
					if (Walk) {
						this->Debug(", ")
					}
				}
			}
			
			this->Debug("= .\n")
		}
		else {
			this->Debug("\n[%i.%i] %x -> ", PITWhole, PITFraction, CurrentTask)
		
			if (DEBUG_SCHEDULER) {
				Task* Walk := RootTask
				
				while (Walk) {
					this->Debug("(%x %i)", Walk, Walk->State)
					
					Walk := Walk->Next
					
					if (Walk) {
						this->Debug(", ")
					}
				}
			}
			
			this->Debug(" = %x {%x %x}\n", Current, Current~>Context->RIP, Current~>Context->RSP)
			CurrentTask := Current
		}
	}
	define void RunNext() {
		this->PickNext()
		this->RunCurrent()
	}
	
	define void YieldCurrent() {
		CurrentTask->State := TASK_PAUSED
		this->RunNext()
	}
	define void BlockCurrent() {
		CurrentTask->State := TASK_BLOCKED
		this->RunNext()
	}
	define void EndCurrent() {
		CurrentTask->State := TASK_KILLED
		CurrentTask->Free()
		this->RunNext()
	}
	
	define Task* OnExternalControlTransfer(Task* NewCurrent) {
		CurrentTask->State := TASK_CAN_RUN
		
		Task* Result := CurrentTask
		CurrentTask := NewCurrent
		
		return Result
	}
	
	define void Remove(Task* Old) {
		Task* Next := RootTask
		
		while (Next->Next) {
			if (Next->Next = Old) {
				Next->Next := Old->Next
				break
			}
		}
	}
	
	define void Enqueue(Task* NewTask) {
		if (RootTask = null) {
			RootTask := NewTask
		}
		else {
			Task* Next := RootTask
			
			while (Next->Next && Next->Priority > NewTask->Priority) {
				Next := Next->Next
			}
			
			NewTask->Next := Next->Next
			Next->Next := NewTask
		}
	}
	
	define void Add(Task* NewTask) {
		this->Enqueue(NewTask)
	}
	
	define Task* MakeLightTask(void* Function) {
		Task* NewTask := Task:NewWithStack(0, 2)
		
		NewTask~>Context->RIP := Function
		
		return NewTask
	}
	define Task* AddLightTask(void* Function) {
		Task* NewTask := this->MakeLightTask(Function)
		
		this->Add(NewTask)
		
		return NewTask
	}
	
	define EventState Quantum(void* CurrentTickCount, Event* Fired) {
		;Info("%x,", CurrentTask~>Context->RIP)
		
		Task* Current := RootTask
		
		while (Current) {
			if (Current->State = TASK_PAUSED) {
				Current->State := TASK_CAN_RUN
			}
			
			Current := Current->Next
		}
		
		this->PickNext()
		
		return EventState:CallNextListener
	}
}

TaskScheduler _Scheduler
TaskScheduler* Scheduler := &_Scheduler

define void _SchedulerYield() {
	Scheduler->YieldCurrent()
}
define void SchedulerYield() asm {
	cli
	
	call, @GetCurrentTaskContext
	mov, rdi, rax      ; Parameter = CurrentTaskContext
	call, @SaveContext ; Capure interrupted context, with anything extra cleaned up below
	
	pop, rbx ; grab return address, store into context
	mov, {rdi} As i64*, rbx        ; Context.RIP = rax
	mov, {rdi + 0x20} As i64*, rsp ; Fix RSP, original capture is off by 8 because of our
	;  return address
	
	jmp, @_SchedulerYield
}

define void _SchedulerBlock() {
	Scheduler->BlockCurrent()
}
define void SchedulerBlock() asm {
	cli
	
	call, @GetCurrentTaskContext
	mov, rdi, rax      ; Parameter = CurrentTaskContext
	call, @SaveContext ; Capure interrupted context, with anything extra cleaned up below
	
	pop, rbx ; grab return address, store into context
	mov, {rdi} As i64*, rbx        ; Context.RIP = rax
	mov, {rdi + 0x20} As i64*, rsp ; Fix RSP, original capture is off by 8 because of our
	;  return address
	
	jmp, @_SchedulerBlock
}
define void SchedulerEnd() {
	Scheduler->EndCurrent()
}
define void SchedulerRestart() {
	; Called after non-task code 
	
	Scheduler->RunCurrent()
}

define void DoSchedulerDelay() {
	loop {
		; Nothing better to do
		asm {
			emit, 0xF4
		}
	}
}

i8 SchedulerStarted := false

define void StartScheduler() {
	DisableInterrupts()
	
	SchedulerStarted := true
	
	DelayTask := Scheduler->MakeLightTask(&DoSchedulerDelay)
	
	OnHardwareTimerTick->ListenMethod(&TaskScheduler.Quantum, Scheduler)
	
	loop {
		asm {
			sti
			emit, 0xF4
		}
	}
}

define i8 IsSchedulerActive() {
	return InterrupsAreEnabled() && SchedulerStarted
}