

define void ATAWait() {
	loop {
		i8 Status := InB(0x1F7)
		
		if (Status & 0x88 = 0x8) || (Status & 0x21 = 0x21) {
			return
		}
	}
}

define void ATAReadWrite(i8 Slave, i32 LBA, i32 Sectors, i8 Command) {
	i8 Device := ((0xE + Slave) << 4) As i8
	i8 HighLBA := ((LBA >> 24) & 0xF) As i8
	
	OutB(0x1F6, Device | HighLBA)
	OutB(0x1F2, Sectors As i8)
	OutB(0x1F3, LBA As i8)
	OutB(0x1F4, (LBA >> 8) As i8)
	OutB(0x1F5, (LBA >> 16) As i8)
	OutB(0x1F7, Command)
}

define void ATARead(i8 FromSlave, i32 LBA, i32 SectorsToRead, void* RawIntoBuffer) {
	i16* IntoBuffer := RawIntoBuffer
	
	ATAReadWrite(FromSlave, LBA, SectorsToRead, 0x20)
	
	for (i32 SectorIndex := 0, SectorIndex < SectorsToRead, SectorIndex += 1) {
		ATAWait()
		
		for (i32 WordIndex := 0, WordIndex < 256, WordIndex += 1) {
			IntoBuffer[WordIndex] := InW(0x1F0)
		}
		
		IntoBuffer += 512
	}
}
define void ATAWrite(i8 ToSlave, i32 LBA, i32 SectorsToWrite, void* FromBuffer) {
	ATAReadWrite(ToSlave, LBA, SectorsToWrite, 0x30)
	
	i16* WordBuffer := FromBuffer As i16*
	
	for (i32 SectorIndex := 0, SectorIndex < SectorsToWrite, SectorIndex += 1) {
		ATAWait()
		
		for (i32 WordIndex := 0, WordIndex < 256, WordIndex += 1) {
			OutW(0x1F0, WordBuffer[WordIndex])
		}
		
		WordBuffer += 512
	}
}

i8{0x200} ATABuffer